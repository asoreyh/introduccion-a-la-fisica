# pause -1 espera a que apretes enter

set xdata time   # cambiar el eje X a tiempo
set timefmt "%s" # decirle que el tiempo viene en Unix Time
set grid

plot 'average.csv' u 1:3 # Flujo vs tiempo
pause -1

plot 'average.csv' u 1:2 # Presión vs tiempo 
pause -1

plot 'average.csv' u 2:3 # Flujo vs presión
pause -1

# Buscamos anticorrelación lineal
flujo(presion)=m*presion+b

# como es anticorrelación, m<0. Fiteamos:
fit flujo(x) 'average.csv' u 2:3 via m,b   # x (columna 2) es la presión

# Resultados del fit:
# Final set of parameters            Asymptotic Standard Error
# =======================            ==========================
# m               = -1.15447         +/- 0.04271      (3.7%)
# b               = 1599.23          +/- 39.22        (2.452%)

# es decir: flujo(presión) = -1.15447 * presión + 1599.23

# veamos como queda el fit:

plot 'average.csv' u 2:3, flujo(x) w l lw 3
pause -1

# y ahora correjimos el efecto de la presión sobre el flujo: 

plot 'average.csv' u 1:($3-flujo($2)) w p
